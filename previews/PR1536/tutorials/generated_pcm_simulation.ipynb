{
 "cells": [
  {
   "cell_type": "markdown",
   "source": [
    "# Running a Multi-Stage Production Cost Simulation\n",
    "\n",
    "> **Set up**\n",
    ">\n",
    "> To run this notebook, first install the Julia kernel for Jupyter Notebooks using [IJulia](https://julialang.github.io/IJulia.jl/stable/manual/installation/), then [create an environment](https://pkgdocs.julialang.org/v1/environments/) for this tutorial with the packages listed with `using <PackageName>` further down.\n",
    ">\n",
    "> This tutorial has demonstrated compatibility with these package versions. If you run into any errors, first check your package versions for consistency using `Pkg.status()`.\n",
    ">\n",
    " > ```\n",
    " > Status `~/work/PowerSimulations.jl/PowerSimulations.jl/docs/Project.toml`\n",
    " >   [336ed68f] CSV v0.10.15\n",
    " >   [a93c6f00] DataFrames v1.8.1\n",
    " >   [864edb3b] DataStructures v0.19.3\n",
    " >   [e30172f5] Documenter v1.16.1\n",
    " >   [d12716ef] DocumenterInterLinks v1.1.0\n",
    " >   [35a29f4d] DocumenterTools v0.1.21\n",
    " >   [87dc4568] HiGHS v1.21.1\n",
    " >   [fc1677e0] HydroPowerSimulations v0.13.1\n",
    " >   [2cd47ed4] InfrastructureSystems v3.3.0\n",
    " >   [4076af6c] JuMP v1.29.4\n",
    " >   [23fbe1c1] Latexify v0.16.10\n",
    " >   [98b081ad] Literate v2.21.0\n",
    " >   [bed98974] PowerNetworkMatrices v0.18.1\n",
    " >   [e690365d] PowerSimulations v0.32.4 `~/work/PowerSimulations.jl/PowerSimulations.jl`\n",
    " >   [f00506e0] PowerSystemCaseBuilder v2.2.0\n",
    " >   [bcd98974] PowerSystems v5.5.0\n",
    " >   [08abe8d2] PrettyTables v3.2.1\n",
    " >   [9e3dc215] TimeSeries v0.25.2\n",
    " > \n",
    " > ```\n",
    "\n",
    "\n",
    "## Introduction\n",
    "\n",
    "PowerSimulations.jl supports simulations that consist of sequential optimization problems\n",
    "where results from previous problems inform subsequent problems in a variety of ways. This\n",
    "example demonstrates some of these capabilities to represent electricity market clearing.\n",
    "This example is intended to be an extension of the tutorial on\n",
    "Running a Simple-Step Problem.\n",
    "\n",
    "### Load Packages"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using PowerSystems\n",
    "using PowerSimulations\n",
    "using HydroPowerSimulations\n",
    "import PowerSimulations as PSI\n",
    "using PowerSystemCaseBuilder\n",
    "using Dates\n",
    "using HiGHS #solver"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "### Optimizer\n",
    "\n",
    "It's most convenient to define an optimizer instance upfront and pass it into the\n",
    "`DecisionModel` constructor. For this example, we can use the free HiGHS solver with a\n",
    "relatively relaxed MIP gap (`ratioGap`) setting to improve speed."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "solver = optimizer_with_attributes(HiGHS.Optimizer, \"mip_rel_gap\" => 0.5)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "> *Note*\n",
    ">\n",
    ">\n",
    "> Defining a solver upfront ensures that only one license is requested when using a license-limited solver, such as Gurobi. We can create a environment variable and pass it to the optimizer constructor for shared license use if using such a solver\n",
    ">\n",
    "> ```julia\n",
    "> using Gurobi\n",
    ">\n",
    "> gurobi_env = Gurobi.Env()\n",
    ">\n",
    "> solver = optimizer_with_attributes(() -> Gurobi.Optimizer(gurobi_env),\"MIPGap\" => 0.01)\n",
    "> ```\n",
    ">\n",
    "> Conversely, if a unique optimizer constructor is defined within the SimulationModels for each stage, a separate license will be obtained for each stage.\n",
    ">\n",
    "### Hourly day-ahead system\n",
    "\n",
    "First, we'll create a `System` with hourly data to represent day-ahead forecasted wind,\n",
    "solar, and load profiles:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "sys_DA = build_system(PSISystems, \"modified_RTS_GMLC_DA_sys\"; skip_serialization = true)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "### 5-Minute system\n",
    "\n",
    "The RTS data also includes 5-minute resolution time series data. So, we can create another\n",
    "`System` to represent 15 minute ahead forecasted data for a \"real-time\" market:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "sys_RT = build_system(PSISystems, \"modified_RTS_GMLC_RT_sys\"; skip_serialization = true)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## `ProblemTemplate`s define stages\n",
    "\n",
    "Sequential simulations in PowerSimulations are created by defining `OperationsProblems`\n",
    "that represent stages, and how information flows between executions of a stage and\n",
    "between different stages.\n",
    "\n",
    "Let's start by defining a two stage simulation that might look like a typical day-Ahead\n",
    "and real-time electricity market clearing process.\n",
    "\n",
    "### Day-ahead unit commitment stage\n",
    "\n",
    "First, we can define a unit commitment template for the day ahead problem. We can use the\n",
    "included UC template, but in this example, we'll replace the `ThermalBasicUnitCommitment`\n",
    "with the slightly more complex `ThermalStandardUnitCommitment` for the thermal generators."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "template_uc = template_unit_commitment()\n",
    "set_device_model!(template_uc, ThermalStandard, ThermalStandardUnitCommitment)\n",
    "set_device_model!(template_uc, HydroDispatch, HydroDispatchRunOfRiver)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "### Define the reference model for the real-time economic dispatch\n",
    "\n",
    "In addition to the manual specification process demonstrated in the OperationsProblem\n",
    "example, PSI also provides pre-specified templates for some standard problems:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "template_ed = template_economic_dispatch(;\n",
    "    network = NetworkModel(PTDFPowerModel; use_slacks = true),\n",
    ")"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "### Define the `SimulationModels`\n",
    "\n",
    "`DecisionModel``s define the problems that are executed in the simulation. The actual problem will change as the stage gets updated to represent different time periods, but the formulations applied to the components is constant within a stage. In this case, we want to define two stages with the `ProblemTemplate`s and the `System`s that we've already created."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "models = SimulationModels(;\n",
    "    decision_models = [\n",
    "        DecisionModel(template_uc, sys_DA; optimizer = solver, name = \"UC\"),\n",
    "        DecisionModel(template_ed, sys_RT; optimizer = solver, name = \"ED\"),\n",
    "    ],\n",
    ")"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "### `SimulationSequence`\n",
    "\n",
    "Similar to a `ProblemTemplate`, the `SimulationSequence` provides a template of\n",
    "how to execute a sequential set of operations problems.\n",
    "\n",
    "Let's review some of the `SimulationSequence` arguments.\n",
    "\n",
    "### Chronologies\n",
    "\n",
    "In PowerSimulations, chronologies define where information is flowing. There are two types\n",
    "of chronologies.\n",
    "\n",
    "  - inter-stage chronologies: Define how information flows between stages. e.g. day-ahead solutions are used to inform economic dispatch problems\n",
    "  - intra-stage chronologies: Define how information flows between multiple executions of a single stage. e.g. the dispatch setpoints of the first period of an economic dispatch problem are constrained by the ramping limits from setpoints in the final period of the previous problem.\n",
    "\n",
    "### `FeedForward`\n",
    "\n",
    "The definition of exactly what information is passed using the defined chronologies is\n",
    "accomplished with `FeedForward`. Specifically, `FeedForward` is used\n",
    "to define what to do with information being passed with an inter-stage chronology. Let's\n",
    "define a `FeedForward` that affects the semi-continuous range constraints of thermal generators\n",
    "in the economic dispatch problems based on the value of the unit-commitment variables."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "feedforward = Dict(\n",
    "    \"ED\" => [\n",
    "        SemiContinuousFeedforward(;\n",
    "            component_type = ThermalStandard,\n",
    "            source = OnVariable,\n",
    "            affected_values = [ActivePowerVariable],\n",
    "        ),\n",
    "    ],\n",
    ")"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "### Sequencing\n",
    "\n",
    "The stage problem length, look-ahead, and other details surrounding the temporal Sequencing\n",
    "of stages are controlled using the structure of the time series data in the `System`s.\n",
    "So, to define a typical day-ahead - real-time sequence:\n",
    "\n",
    "  - Day ahead problems should represent 48 hours, advancing 24 hours after each execution (24-hour look-ahead)\n",
    "  - Real time problems should represent 1 hour (12 5-minute periods), advancing 15 min after each execution (15 min look-ahead)\n",
    "\n",
    "We can adjust the time series data to reflect this structure in each `System`:\n",
    "\n",
    "  - `transform_single_time_series!(sys_DA, Hour(48), Hour(24))`\n",
    "  - `transform_single_time_series!(sys_RT, Minute(60), Minute(15))`\n",
    "\n",
    "Now we can put it all together to define a `SimulationSequence`"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "DA_RT_sequence = SimulationSequence(;\n",
    "    models = models,\n",
    "    ini_cond_chronology = InterProblemChronology(),\n",
    "    feedforwards = feedforward,\n",
    ")"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## `Simulation`\n",
    "\n",
    "Now, we can build and execute a simulation using the `SimulationSequence` and `Stage`s\n",
    "that we've defined."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "path = mkdir(joinpath(\".\", \"rts-store\")) #hide\n",
    "sim = Simulation(;\n",
    "    name = \"rts-test\",\n",
    "    steps = 2,\n",
    "    models = models,\n",
    "    sequence = DA_RT_sequence,\n",
    "    simulation_folder = joinpath(\".\", \"rts-store\"),\n",
    ")"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "### Build simulation"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "build!(sim)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "### Execute simulation\n",
    "\n",
    "the following command returns the status of the simulation (0: is proper execution) and\n",
    "stores the results in a set of HDF5 files on disk."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "execute!(sim; enable_progress_bar = false)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Results\n",
    "\n",
    "To access the results, we need to load the simulation result metadata and then make\n",
    "requests to the specific data of interest. This allows you to efficiently access the\n",
    "results of interest without overloading resources."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "results = SimulationResults(sim);\n",
    "uc_results = get_decision_problem_results(results, \"UC\"); # UC stage result metadata\n",
    "ed_results = get_decision_problem_results(results, \"ED\"); # ED stage result metadata"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "We can read all the result variables"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "read_variables(uc_results)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "or all the parameters"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "read_parameters(uc_results)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "We can just list the variable names contained in `uc_results`:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "list_variable_names(uc_results)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "and a number of parameters (this pattern also works for aux_variables, expressions, and duals)"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "list_parameter_names(uc_results)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "Now we can read the specific results of interest for a specific problem, time window (optional),\n",
    "and set of variables, duals, or parameters (optional)"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "Dict([\n",
    "    v => read_variable(uc_results, v) for v in [\n",
    "        \"ActivePowerVariable__RenewableDispatch\",\n",
    "        \"ActivePowerVariable__HydroDispatch\",\n",
    "        \"StopVariable__ThermalStandard\",\n",
    "    ]\n",
    "])"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "Or if we want the result of just one variable, parameter, or dual (must be defined in the\n",
    "problem definition), we can use:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "read_parameter(\n",
    "    ed_results,\n",
    "    \"ActivePowerTimeSeriesParameter__RenewableNonDispatch\";\n",
    "    initial_time = DateTime(\"2020-01-01T06:00:00\"),\n",
    "    count = 5,\n",
    ")"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "> *Info*\n",
    ">\n",
    ">\n",
    "note that this returns the results of each execution step in a separate dataframe\n",
    "If you want the realized results (without lookahead periods), you can call `read_realized_*`:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "read_realized_variables(\n",
    "    uc_results,\n",
    "    [\"ActivePowerVariable__ThermalStandard\", \"ActivePowerVariable__RenewableDispatch\"],\n",
    ")\n",
    "rm(path; force = true, recursive = true) #hide"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Plotting\n",
    "\n",
    "Take a look at the plotting capabilities in [PowerGraphics.jl](https://nrel-sienna.github.io/PowerGraphics.jl/stable/)"
   ],
   "metadata": {}
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.5"
  },
  "kernelspec": {
   "name": "julia-1.12",
   "display_name": "Julia 1.12.5",
   "language": "julia"
  }
 },
 "nbformat": 4
}
