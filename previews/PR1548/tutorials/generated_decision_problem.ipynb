{
 "cells": [
  {
   "cell_type": "markdown",
   "source": [
    "# Running a Simple-Step Problem\n",
    "\n",
    "> **Set up**\n",
    ">\n",
    "> To run this notebook, first install the Julia kernel for Jupyter Notebooks using [IJulia](https://julialang.github.io/IJulia.jl/stable/manual/installation/), then [create an environment](https://pkgdocs.julialang.org/v1/environments/) for this tutorial with the packages listed with `using <PackageName>` further down.\n",
    ">\n",
    "> This tutorial has demonstrated compatibility with these package versions. If you run into any errors, first check your package versions for consistency using `Pkg.status()`.\n",
    ">\n",
    " > ```\n",
    " > Status `~/work/PowerSimulations.jl/PowerSimulations.jl/docs/Project.toml`\n",
    " >   [336ed68f] CSV v0.10.16\n",
    " >   [a93c6f00] DataFrames v1.8.1\n",
    " >   [864edb3b] DataStructures v0.19.3\n",
    " >   [e30172f5] Documenter v1.17.0\n",
    " >   [d12716ef] DocumenterInterLinks v1.1.0\n",
    " >   [35a29f4d] DocumenterTools v0.1.21\n",
    " >   [87dc4568] HiGHS v1.21.1\n",
    " >   [fc1677e0] HydroPowerSimulations v0.14.0\n",
    " >   [2cd47ed4] InfrastructureSystems v3.3.1\n",
    " >   [4076af6c] JuMP v1.29.4\n",
    " >   [23fbe1c1] Latexify v0.16.10\n",
    " >   [98b081ad] Literate v2.21.0\n",
    " >   [bed98974] PowerNetworkMatrices v0.18.1\n",
    " >   [e690365d] PowerSimulations v0.33.0 `~/work/PowerSimulations.jl/PowerSimulations.jl`\n",
    " >   [f00506e0] PowerSystemCaseBuilder v2.2.0\n",
    " >   [bcd98974] PowerSystems v5.5.0\n",
    " >   [08abe8d2] PrettyTables v3.2.3\n",
    " >   [9e3dc215] TimeSeries v0.25.2\n",
    " > \n",
    " > ```\n",
    "\n",
    "\n",
    "## Introduction\n",
    "\n",
    "`PowerSimulations.jl` supports the construction and solution of optimal power system\n",
    "scheduling problems (Operations Problems). Operations problems form the fundamental\n",
    "building blocks for sequential simulations. This example shows how to specify and customize\n",
    "the mathematics that will be applied to the data with a `ProblemTemplate`,\n",
    "build and execute a `DecisionModel`, and access the results."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using PowerSystems\n",
    "using PowerSimulations\n",
    "using HydroPowerSimulations\n",
    "using PowerSystemCaseBuilder\n",
    "using HiGHS # solver\n",
    "using Dates"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Data\n",
    "\n",
    "> *Note*\n",
    ">\n",
    ">\n",
    "> [PowerSystemCaseBuilder.jl](https://github.com/NREL-Sienna/PowerSystemCaseBuilder.jl)\n",
    "> is a helper library that makes it easier to reproduce examples in the documentation\n",
    "> and tutorials. Normally you would pass your local files to create the system data\n",
    "> instead of calling the function `build_system`.\n",
    "> For more details visit\n",
    "> [PowerSystemCaseBuilder Documentation](https://nrel-sienna.github.io/PowerSystems.jl/stable/how_to/powersystembuilder/)"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "sys = build_system(PSISystems, \"modified_RTS_GMLC_DA_sys\")"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Define a problem specification with a `ProblemTemplate`\n",
    "\n",
    "You can create an empty template with:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "template_uc = ProblemTemplate()"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "Now, you can add a `DeviceModel` for each device type to create an assignment\n",
    "between PowerSystems device types and the subtypes of `AbstractDeviceFormulation`.\n",
    "PowerSimulations has a variety of different `AbstractDeviceFormulation` subtypes\n",
    "that can be applied to different PowerSystems device types, each dispatching to different\n",
    "methods for populating optimization problem objectives, variables, and constraints.\n",
    "Documentation on the formulation options for various devices can be found in the\n",
    "[formulation library docs](https://nrel-sienna.github.io/PowerSimulations.jl/latest/formulation_library/General/#formulation_library)"
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "### Branch Formulations\n",
    "\n",
    "Here is an example of relatively standard branch formulations. Other formulations allow\n",
    "for selective enforcement of transmission limits and greater control on transformer settings."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "set_device_model!(template_uc, Line, StaticBranch)\n",
    "set_device_model!(template_uc, Transformer2W, StaticBranch)\n",
    "set_device_model!(template_uc, TapTransformer, StaticBranch)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "### Injection Device Formulations\n",
    "\n",
    "Here we define template entries for all devices that inject or withdraw power on the\n",
    "network. For each device type, we can define a distinct `AbstractDeviceFormulation`. In\n",
    "this case, we're defining a basic unit commitment model for thermal generators,\n",
    "curtailable renewable generators, and fixed dispatch (net-load reduction) formulations\n",
    "for `HydroDispatch` and `RenewableNonDispatch` devices."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "set_device_model!(template_uc, ThermalStandard, ThermalStandardUnitCommitment)\n",
    "set_device_model!(template_uc, RenewableDispatch, RenewableFullDispatch)\n",
    "set_device_model!(template_uc, PowerLoad, StaticPowerLoad)\n",
    "set_device_model!(template_uc, HydroDispatch, HydroDispatchRunOfRiver)\n",
    "set_device_model!(template_uc, RenewableNonDispatch, FixedOutput)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "### Service Formulations\n",
    "\n",
    "We have two `VariableReserve` types, parameterized by their direction. So, similar to\n",
    "creating `DeviceModel`s, we can create `ServiceModel`s. The primary difference being\n",
    "that `DeviceModel` objects define how constraints get created, while `ServiceModel` objects\n",
    "define how constraints get modified."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "set_service_model!(template_uc, VariableReserve{ReserveUp}, RangeReserve)\n",
    "set_service_model!(template_uc, VariableReserve{ReserveDown}, RangeReserve)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "### Network Formulations\n",
    "\n",
    "Finally, we can define the transmission network specification that we'd like to model.\n",
    "For simplicity, we'll choose a copper plate formulation. But there are dozens of\n",
    "specifications available through an integration with\n",
    "[PowerModels.jl](https://lanl-ansi.github.io/PowerModels.jl/stable/).\n",
    "\n",
    "*Note that many formulations will require appropriate data and may be computationally intractable*"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "set_network_model!(template_uc, NetworkModel(CopperPlatePowerModel))"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## `DecisionModel`\n",
    "\n",
    "Now that we have a `System` and a `ProblemTemplate`, we can put the two together\n",
    "to create a `DecisionModel` that we solve."
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "### Optimizer\n",
    "\n",
    "It's most convenient to define an optimizer instance upfront and pass it into the\n",
    "`DecisionModel` constructor. For this example, we can use the free HiGHS solver\n",
    "with a relatively relaxed MIP gap (`ratioGap`) setting to improve speed."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "solver = optimizer_with_attributes(HiGHS.Optimizer, \"mip_rel_gap\" => 0.5)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "### Build a `DecisionModel`\n",
    "\n",
    "The construction of a `DecisionModel` essentially applies a `ProblemTemplate`\n",
    "to `System` data to create a JuMP model."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "problem = DecisionModel(template_uc, sys; optimizer = solver, horizon = Hour(24))\n",
    "build!(problem; output_dir = mktempdir())"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "> *Tip*\n",
    ">\n",
    ">\n",
    "> The principal component of the `DecisionModel` is the JuMP model.\n",
    "> But you can serialize to a file using the following command:\n",
    ">\n",
    "> ```julia\n",
    "> serialize_optimization_model(problem, save_path)\n",
    "> ```\n",
    ">\n",
    "> Keep in mind that if the setting `\"store_variable_names\"` is set to `False` then\n",
    "> the file won't show the model's names."
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "### Solve a `DecisionModel`"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "solve!(problem)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Results Inspection\n",
    "\n",
    "PowerSimulations collects the `DecisionModel` results into a\n",
    "`OptimizationProblemResults` struct:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "res = OptimizationProblemResults(problem)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "### Optimizer Stats\n",
    "\n",
    "The optimizer summary is included"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "get_optimizer_stats(res)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "### Objective Function Value"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "get_objective_value(res)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "### Variable, Parameter, Auxiliary Variable, Dual, and Expression Values\n",
    "\n",
    "The solution value data frames for variables, parameters, auxiliary variables, duals, and\n",
    "expressions can be accessed using the `read_` methods:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "read_variables(res)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "Or, you can read a single parameter value for parameters that exist in the results."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "list_parameter_names(res)\n",
    "read_parameter(res, \"ActivePowerTimeSeriesParameter__RenewableDispatch\")"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Plotting\n",
    "\n",
    "Take a look at the plotting capabilities in\n",
    "[PowerGraphics.jl](https://nrel-sienna.github.io/PowerGraphics.jl/stable/)"
   ],
   "metadata": {}
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.5"
  },
  "kernelspec": {
   "name": "julia-1.12",
   "display_name": "Julia 1.12.5",
   "language": "julia"
  }
 },
 "nbformat": 4
}
