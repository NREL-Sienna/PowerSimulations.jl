function check_folder_integrity(folder::String)
    folder_files = readdir(folder)
    alien_files = setdiff(folder_files, KNOWN_SIMULATION_PATHS)
    alien_files = filter(x -> !any(occursin.(IGNORABLE_FILES, x)), alien_files)
    if isempty(alien_files)
        return true
    else
        @warn "Unrecognized simulation files: $(sort(alien_files))"
    end
    if STORE_DIR ∉ folder_files
        error("The file path doesn't contain any data_store folder")
    end
    return false
end

struct SimulationResults
    path::String
    params::SimulationStoreParams
    decision_problem_results::Dict{
        String,
        SimulationProblemResults{DecisionModelSimulationResults},
    }
    emulation_problem_results::SimulationProblemResults{EmulationModelSimulationResults}
    store::Union{Nothing, SimulationStore}
end

function SimulationResults(path::AbstractString, execution = nothing; ignore_status = false)
    # This method maintains compatibility with the old interface as long as there is only
    # one simulation name.
    unique_names = Set{String}()
    for name in readdir(path)
        m = match(r"(.*)-\d+$", name)
        if isnothing(m)
            push!(unique_names, name)
        else
            push!(unique_names, m.captures[1])
        end
    end

    if length(unique_names) == 1
        name = first(unique_names)
        return SimulationResults(path, name, execution; ignore_status = ignore_status)
    end

    if STORE_DIR in readdir(path)
        return SimulationResults(
            dirname(path),
            basename(path),
            execution;
            ignore_status = ignore_status,
        )
    end

    error(
        "Found more than one simulation name in $path. Please call the constructor that includes 'name.'",
    )
end

"""
Construct SimulationResults from a simulation output directory.

# Arguments

  - `path::AbstractString`: Simulation output directory
  - `name::AbstractString`: Simulation name
  - `execution::AbstractString`: Execution number. Default is the most recent.
  - `ignore_status::Bool`: If true, return results even if the simulation failed.
"""
_emulation_system(::Nothing) = nothing
_emulation_system(model::EmulationModel) = get_system(model)

function SimulationResults(
    path::AbstractString,
    name::AbstractString,
    execution = nothing;
    ignore_status = false,
)
    if isnothing(execution)
        execution = _get_most_recent_execution(path, name)
    end
    if execution == 1
        execution_path = joinpath(path, name)
    else
        execution_path = joinpath(path, "$name-$execution")
    end
    if !isdir(execution_path)
        error("No valid simulation in $execution_path: execution = $execution")
    end

    @info "Loading simulation results from $execution_path"
    status = deserialize_status(joinpath(execution_path, RESULTS_DIR))
    _check_status(status, ignore_status)

    if !check_folder_integrity(execution_path)
        @warn "The results folder $(execution_path) is not consistent with the default folder structure. " *
              "This can lead to errors or unwanted results."
    end

    simulation_store_path = joinpath(execution_path, STORE_DIR)
    check_file_integrity(simulation_store_path)

    return open_store(HdfSimulationStore, simulation_store_path, "r") do store
        decision_problem_results =
            Dict{String, SimulationProblemResults{DecisionModelSimulationResults}}()
        sim_params = get_params(store)
        container_key_lookup = get_container_key_lookup(store)
        for (name, problem_params) in sim_params.decision_models_params
            name = string(name)
            system = if has_system(store, get_system_uuid(problem_params))
                deserialize_system(store, get_system_uuid(problem_params))
            else
                nothing
            end
            problem_result = SimulationProblemResults(
                DecisionModel,
                store,
                name,
                problem_params,
                sim_params,
                execution_path,
                container_key_lookup;
                system = system,
            )
            decision_problem_results[name] = problem_result
        end

        em_params = get_emulation_model_params(sim_params)
        em_system = if has_system(store, get_system_uuid(em_params))
            deserialize_system(store, get_system_uuid(em_params))
        else
            nothing
        end
        emulation_result = SimulationProblemResults(
            EmulationModel,
            store,
            string(first(keys(sim_params.emulation_model_params))),
            em_params,
            sim_params,
            execution_path,
            container_key_lookup;
            system = em_system,
        )

        return SimulationResults(
            execution_path,
            sim_params,
            decision_problem_results,
            emulation_result,
            nothing,
        )
    end
end

"""
Construct SimulationResults from a simulation.
"""
function SimulationResults(sim::Simulation; ignore_status = false, kwargs...)
    _check_status(get_simulation_status(sim), ignore_status)
    store = get_simulation_store(sim)
    execution_path = get_simulation_dir(sim)
    decision_problem_results =
        Dict{String, SimulationProblemResults{DecisionModelSimulationResults}}()
    sim_params = get_params(store)
    models = get_models(sim)
    container_key_lookup = get_container_key_lookup(store)
    for (name, problem_params) in sim_params.decision_models_params
        model = get_simulation_model(models, name)
        name = string(name)
        problem_result = SimulationProblemResults(
            DecisionModel,
            store,
            name,
            problem_params,
            sim_params,
            execution_path,
            container_key_lookup;
            system = get_system(model),
        )
        decision_problem_results[name] = problem_result
    end

    emulation_model = get_emulation_model(models)
    emulation_results = SimulationProblemResults(
        EmulationModel,
        store,
        string(first(keys(sim_params.emulation_model_params))),
        first(values(sim_params.emulation_model_params)),
        sim_params,
        execution_path,
        container_key_lookup;
        system = _emulation_system(emulation_model),
    )

    return SimulationResults(
        execution_path,
        sim_params,
        decision_problem_results,
        emulation_results,
        store,
    )
end

"""
    Base.empty!(res::SimulationResults)

Empty the [`SimulationResults`](@ref)
"""
function Base.empty!(res::SimulationResults)
    foreach(empty!, values(res.decision_problem_results))
    empty!(res.emulation_problem_results)
end

Base.isempty(res::SimulationResults) = all(isempty, values(res.decision_problem_results))
Base.length(res::SimulationResults) =
    mapreduce(length, +, values(res.decision_problem_results))

"""
Return SimulationProblemResults corresponding to a SimulationResults

# Arguments
 - `sim_results::PSI.SimulationResults`: the simulation results to read from
 - `problem::String`: the name of the problem (e.g., "UC", "ED")
 - `populate_system::Bool = true`: whether to set the results' system as if using
   [`get_system!`](@ref)
 - `populate_units::Union{IS.UnitSystem, String, Nothing} = nothing`: unsupported;
   PowerSystems (psy6) has no system-wide unit base, so passing a non-`nothing`
   value throws (requires `populate_system=true`)
"""
function get_decision_problem_results(
    results::SimulationResults,
    problem::String;
    populate_system::Bool = false,
    populate_units::Union{IS.UnitSystem, String, Nothing} = nothing,
)
    if !haskey(results.decision_problem_results, problem)
        throw(IS.InvalidValue("$problem is not stored"))
    end

    results = results.decision_problem_results[problem]
    _populate_system_in_results!(results, populate_system, populate_units)

    return results
end

"""
Return SimulationProblemResults corresponding to a SimulationResults

# Arguments
 - `sim_results::PSI.SimulationResults`: the simulation results to read from
 - `populate_system::Bool = true`: whether to set the results' system as if using
   [`get_system!`](@ref)
 - `populate_units::Union{IS.UnitSystem, String, Nothing} = nothing`: unsupported;
   PowerSystems (psy6) has no system-wide unit base, so passing a non-`nothing`
   value throws (requires `populate_system=true`)
"""
function get_emulation_problem_results(
    results::SimulationResults;
    populate_system::Bool = false,
    populate_units::Union{IS.UnitSystem, String, Nothing} = nothing,
)
    results = results.emulation_problem_results
    _populate_system_in_results!(results, populate_system, populate_units)
    return results
end

function _populate_system_in_results!(
    results::SimulationProblemResults,
    populate_system::Bool,
    populate_units::Union{IS.UnitSystem, String, Nothing},
)
    if populate_system
        try
            get_system!(results)
        catch e
            error("Can't find the system file or retrieve the system error=$e")
        end

        # PowerSystems (psy6) removed the system-wide unit-base mode this used to set via
        # `set_units_base_system!`; getters now take an explicit unit system (PSY.SU/DU/NU)
        # per call. Error loudly rather than silently ignoring a caller's request.
        if !isnothing(populate_units)
            error(
                "populate_units is not supported: PowerSystems no longer has a system-wide " *
                "unit base. Pass the desired unit system explicitly to each accessor instead " *
                "(e.g. PSY.get_rating(component, PSY.SU)).",
            )
        end

    else
        (isnothing(populate_units)) ||
            throw(
                ArgumentError(
                    "populate_units=$populate_units is unaccepted when populate_system=$populate_system",
                ),
            )
    end
    return
end

"""
Return the problem names in the simulation.
"""
list_decision_problems(results::SimulationResults) =
    collect(keys(results.decision_problem_results))

"""
Export results to files in the results directory.

# Arguments

  - `results::SimulationResults`: simulation results
  - `exports`: SimulationResultsExport or anything that can be passed to its constructor.
    (such as Dict or path to JSON file)

An example JSON file demonstrating possible options is below. Note that `start_time`,
`end_time`, `path`, and `format` are optional.

```
{
  "decision_models": [
    {
      "name": "ED",
      "variables": [
        "P__ThermalStandard",
      ],
      "parameters": [
        "all"
      ]
    },
    {
      "name": "UC",
      "variables": [
        "On__ThermalStandard"
      ],
      "parameters": [
        "all"
      ],
      "duals": [
        "all"
      ]
    }
  ],
  "start_time": "2020-01-01T04:00:00",
  "end_time": null,
  "path": null,
  "format": "csv"
}

```
"""
function export_results(results::SimulationResults, exports)
    _export_results_with_store(results, exports, results.store)
    return
end

_export_results_with_store(results, exports, store::InMemorySimulationStore) =
    export_results(results, exports, store)
function _export_results_with_store(results, exports, ::Union{Nothing, HdfSimulationStore})
    _open_results_store(results.path) do store
        export_results(results, exports, store)
    end
    return
end

function export_results(results::SimulationResults, exports, store::SimulationStore)
    exports = _as_results_export(exports, results.params)
    file_type = get_export_file_type(exports)

    for problem_results in values(results.decision_problem_results)
        problem_exports = get_problem_exports(exports, problem_results.problem)
        if isnothing(exports.path)
            path = problem_results.results_output_folder
        else
            path = exports.path
        end
        for timestamp in get_timestamps(problem_results)
            !should_export(exports, timestamp) && continue
            for (folder, list_names, should_export_fn, read_fn) in (
                ("variables", list_variable_names, should_export_variable, read_variable),
                (
                    "aux_variables",
                    list_aux_variable_names,
                    should_export_aux_variable,
                    read_aux_variable,
                ),
                (
                    "parameters",
                    list_parameter_names,
                    should_export_parameter,
                    read_parameter,
                ),
                ("duals", list_dual_names, should_export_dual, read_dual),
                (
                    "expression",
                    list_expression_names,
                    should_export_expression,
                    read_expression,
                ),
            )
                export_path = mkpath(joinpath(path, problem_results.problem, folder))
                for name in list_names(problem_results)
                    should_export_fn(problem_exports, name) || continue
                    dfs = read_fn(
                        problem_results,
                        name;
                        start_time = timestamp,
                        len = 1,
                        store = store,
                    )
                    export_output(file_type, export_path, name, timestamp, dfs[timestamp])
                end
            end
        end

        if problem_exports.optimizer_stats
            export_path = joinpath(path, problem_results.problem, "optimizer_stats.csv")
            df = read_optimizer_stats(problem_results; store = store)
            export_output(file_type, export_path, df)
        end
    end
    return
end

function _check_status(status::RunStatus, ignore_status)
    status == RunStatus.SUCCESSFULLY_FINALIZED && return

    if ignore_status
        @warn "Simulation was not successful: $status. Results may not be valid."
    else
        error(
            "Simulation was not successful: status = $status. Set ignore_status = true to override.",
        )
    end
    return
end
